alpha = _{ 'a'..'z' | 'A'..'Z' }
digit = _{ '0'..'9' }
number = { digit+ }
float = { digit+ ~ "." ~ digit+ }
special_charaters = _{ "_" | "-" | "\\" }
whitespc = _{ " "* }
lparen = _{"("}
rparen = _{")"}
at = _{ "@" }
path = { ">" }
reverse_path = { "<" }
asterisk = _{ "*" }
slash = { "/" }
double_dot = _{ ".." }
ident = { (alpha | digit | special_charaters)+ }
_as = _{ "as" }
as = { (" ")* ~ _as ~ (" ")* ~ literal }
sharp = _{ "#" }
dot = _{ "." }
greater = { ">" }
less = { "<" }
greater_equal = { ">=" }
less_equal = { "<=" }
equal = { "==" }
cmp = { equal | greater | less | greater_equal | less_equal }
true_bool = { "true" }
false_bool = { "false" }
truthy = { true_bool | false_bool }
string = { (alpha | digit | special_charaters | " " | "%" | "{" | "}" | "\\" )+ ~ (whitespc ~ string)* }
literal = { "'" ~ string ~ "'" }
literal_keyed = { literal ~ as? }
grouped_literal = { lparen ~ whitespc ~ literal ~ (whitespc ~ ("|") ~ whitespc ~ literal)* ~ rparen }

child = { slash ~ ident }
any_child = { slash ~ asterisk }
descendant_child = { slash ~ double_dot ~ ident }
grouped_any = { slash ~ grouped_literal }
array_index = { slash ~ "[" ~ number ~ "]" }
slice = { slash ~ "[" ~ number ~ ":" ~ number ~ "]" }
array_to = { slash ~ "[:" ~ number ~ "]" }
array_from = { slash ~ "[" ~ number ~ ":]" }
pick = {
    sharp ~ "pick" ~ (" ")* ~
    "(" ~ (literal_keyed | (sub_expression_keyed | sub_expression_keyed_reversed) ) ~
    ((",") ~ (" ")* ~ (literal_keyed | (sub_expression_keyed | sub_expression_keyed_reversed) ))* ~
    (" ")* ~ ")"
}

sub_expression = { path ~ (formatsFn | lenFn | allFn | pickFn | sumFn | filterFn | child | any_child | grouped_any | descendant_child | array_index | slice | array_to | array_from)* }
sub_expression_reversed = { reverse_path ~ (formatsFn | lenFn | allFn | pickFn | sumFn | filterFn | grouped_any | child | any_child | descendant_child | array_index | slice | array_to | array_from)* }
sub_expression_keyed = { sub_expression ~ as? }
sub_expression_keyed_reversed = { sub_expression_reversed ~ as? }

array = {
    "[" ~
    (" ")* ~
    (
	float ~ ((",") ~ (" ")* ~ float)* |
	number ~ ((",") ~ (" ")* ~ number)* |
	literal ~ ((",") ~ (" ")* ~ literal)*
    ) ~
    (" ")* ~
    "]"
}

squash = { sharp ~ "squash" ~ "(" ~ ")" }
sum = { sharp ~ "sum" }
len = { sharp ~ "len" }
all = { sharp ~ "all" }
allFn = { slash ~ all }
lenFn = { slash ~ len }
sumFn = { slash ~ sum }
squashFn = { slash ~ squash }
pickFn = { slash ~ pick }
formats = { sharp ~ "formats" ~ whitespc ~ lparen ~ literal ~ ((",")* ~ whitespc ~ literal)+ ~ whitespc ~ rparen ~ whitespc ~ as}
formatsFn = { slash ~ formats }
filter = { sharp ~ "filter" ~ lparen ~ whitespc ~ ( filter_elem ~ whitespc ~ cmp ~ whitespc ~ (truthy | literal) ) ~ whitespc ~ rparen }
filter_elem = { literal }
filterFn = { slash ~ filter }

expression = {
    (path|reverse_path) ~
    (formatsFn | lenFn | allFn | pickFn | sumFn | filterFn | grouped_any | child | any_child | descendant_child | array_index | slice | array_to | array_from)* ~
    EOI
}
