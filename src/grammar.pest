alpha = _{ 'a'..'z' | 'A'..'Z' }
digit = _{ '0'..'9' }
number = { digit+ }
float = { digit+ ~ "." ~ digit+ }
special_charaters = _{ "_" | "-" | "\\" }
whitespace = _{ (" "| "\n") * }
lparen = _{"("}
rparen = _{")"}
at = _{ "@" }
path = { ">" }
reverse_path = { "<" }
asterisk = _{ "*" }
slash = { "/" }
double_dot = _{ ".." }
ident = { (alpha | digit | special_charaters)+ }
_as = _{ "as" }
_arrow = _{ "->" }
as = { (" ")* ~ _as ~ (" ")* ~ literal }
_asDeref = _{ "as*"}
_arrowDeref = _{"->*"}
asDeref = { (" ")* ~ _asDeref ~ (" ")* ~ literal }
arrow = { (" ")* ~ _arrow ~ (" ")* ~ literal }
arrowDeref = { (" ")* ~ _arrowDeref ~ (" ")* ~ literal }
sharp = _{ "#" }
dot = _{ "." }
greater = { ">" }
less = { "<" }
greater_equal = { ">=" }
less_equal = { "<=" }
equal = { "==" }
almost = { "~=" }
not_euql = { "!=" }
cmp = { almost | not_euql | greater_equal | less_equal | equal | greater | less }
true_bool = { "true" }
false_bool = { "false" }
truthy = { true_bool | false_bool }
string = { (alpha | digit | special_charaters | " " | "%" | "{" | "}" | "\\" )+ ~ (whitespace ~ string)* }
literal = { "'" ~ string ~ "'" }
literal_keyed = { literal ~ as? }
grouped_literal = { lparen ~ whitespace ~ literal ~ (whitespace ~ ("|") ~ whitespace ~ literal)* ~ rparen }
and = { "and" }
or = { "or" }
logical_cmp = { and | or }
child = { slash ~ ident }
any_child = { slash ~ asterisk }
descendant_child = { slash ~ double_dot ~ ident }
grouped_any = { slash ~ grouped_literal }
array_index = { slash ~ "[" ~ number ~ "]" }
slice = { slash ~ "[" ~ number ~ ":" ~ number ~ "]" }
array_to = { slash ~ "[:" ~ number ~ "]" }
array_from = { slash ~ "[" ~ number ~ ":]" }
pick = {
    sharp ~ "pick" ~ (" ")* ~
    "(" ~ (literal_keyed | (sub_expression_keyed | sub_expression_keyed_reversed) ) ~
    ((",") ~ (" ")* ~ (literal_keyed | (sub_expression_keyed | sub_expression_keyed_reversed) ))* ~
    (" ")* ~ ")"
}
sub_expression = { path ~ (allFn | pickFn | filterFn | child | any_child | grouped_any | descendant_child | array_index | slice | array_to | array_from | fn)* }
sub_expression_reversed = { reverse_path ~ (allFn | pickFn | filterFn | grouped_any | child | any_child | descendant_child | array_index | slice | array_to | array_from | fn )* }
sub_expression_keyed = { sub_expression ~ as? }
sub_expression_keyed_reversed = { sub_expression_reversed ~ as? }

all = { sharp ~ "all" }
allFn = { slash ~ all }
pickFn = { slash ~ pick }
fnLit = { literal }
fnExpr = { sub_expression }
fnCall = { sharp ~ ident ~ (whitespace ~ lparen ~ ((filterStmtCollection|fnLit|fnExpr ) ~ whitespace ~ ((",")* ~ whitespace ~ (filterStmtCollection|fnLit|fnExpr))*)? ~ whitespace ~ rparen ~ whitespace)? ~ (arrow | arrowDeref)? }
fn = {slash ~ fnCall}
filterStmt = { ( filter_elem ~ whitespace ~ cmp ~ whitespace ~ (float | truthy | literal | number ) ) }
filterStmtCollection = { filterStmt ~ whitespace ~ (logical_cmp ~ whitespace ~ filterStmt~ whitespace)*  }
filter = { sharp ~ "filter" ~ lparen ~ whitespace ~ ( filterStmt ~ whitespace ~ (logical_cmp ~ whitespace ~ filterStmt~ whitespace)* ) ~ whitespace ~ rparen }
filter_elem = { literal }
filterFn = { slash ~ filter }

expression = {
    (path|reverse_path) ~
    (allFn | pickFn | filterFn | grouped_any | child | any_child | descendant_child | array_index | slice | array_to | array_from | fn)* ~
    EOI
}
